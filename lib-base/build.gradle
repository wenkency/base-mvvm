plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    // 远程仓库
    id 'maven-publish'
}
def libVersion = "1.9.2"
def libCode = 192
group = 'com.github.wenkency'
version = libVersion
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.wenkency'
                artifactId = 'android-mvvm'
                version = libVersion
            }
        }
    }
}
android {
    compileSdkVersion this.app.compileSdkVersion

    defaultConfig {
        minSdkVersion this.app.minSdkVersion
        targetSdkVersion this.app.targetSdkVersion
        versionCode libCode
        versionName libVersion
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    // 这个是使用dataBinding
    buildFeatures {
        dataBinding true
    }
}

// activity统一版本
configurations.all {
    resolutionStrategy {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'androidx.activity') {
                details.useVersion "1.4.0"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    // appcompat
    compileOnly "androidx.appcompat:appcompat:1.4.2"
    // ViewModel LiveData
    def lifecycle_version = "2.4.1"
    compileOnly "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    compileOnly "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // kt core
    compileOnly "androidx.core:core-ktx:1.7.0"
    compileOnly "androidx.constraintlayout:constraintlayout:2.1.3"
    // loading title
    compileOnly "com.github.wenkency:loading:1.5.0"
    compileOnly "com.github.wenkency:titlebar:2.0.8"
}
